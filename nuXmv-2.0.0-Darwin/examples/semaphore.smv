MODULE main
VAR
  semaphore : boolean;
  proc1 : process user(semaphore);
  proc2 : process user(semaphore);
ASSIGN
  init(semaphore) := FALSE;

CTLSPEC
  AG ! (proc1.state = critical & proc2.state = critical);

LTLSPEC
  G ! (proc1.state = critical & proc2.state = critical);
  
CTLSPEC
  AG (proc1.state = entering -> AF proc1.state = critical);

LTLSPEC
  G (proc1.state = entering -> F proc1.state = critical);

MODULE user(semaphore)
VAR
  state : {idle,entering,critical,exiting};
ASSIGN
  init(state) := idle;
  next(state) := 
    case
      state = idle : {idle,entering};
      state = entering & !semaphore : critical;
      state = critical : {critical,exiting};
      state = exiting : idle;
      TRUE : state; -- else: stay the same: next(state) = state
    esac;
  next(semaphore) := 
    case
      state = entering : TRUE;
      state = exiting : FALSE;
      TRUE : semaphore;
    esac;
FAIRNESS
  running
